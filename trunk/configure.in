# $Id: configure.in,v 1.17 2001-09-25 12:26:48 thep Exp $
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.50)

AC_INIT(include/thai/thailib.h)
AM_INIT_AUTOMAKE(libthai, 0.1.1)

AC_SUBST(VERSION)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL

dnl Check commandline options
AC_ARG_ENABLE(debug,
              [  --enable-debug          enable assertion checks [[default=yes]]],
              , enable_debug="yes")
AC_ARG_ENABLE(ansi,
              [  --enable-ansi           turn on strict ansi [[default=no]]],
              , enable_ansi="no")

if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[[\ \	]]-Wall[[\ \	]]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac

  if test "x$enable_ansi" = "xyes"; then
    case " $CFLAGS " in
    *[[\ \	]]-ansi[[\ \	]]*) ;;
    *) CFLAGS="$CFLAGS -ansi" ;;
    esac

    case " $CFLAGS " in
    *[[\ \	]]-pedantic[[\ \	]]*) ;;
    *) CFLAGS="$CFLAGS -pedantic" ;;
    esac
  fi
fi
if test "x$enable_debug" = "xno"; then
  case " $CFLAGS " in
  *[[\ \	]]-DNDEBUG[[\ \	]]*) ;;
  *) CFLAGS="$CFLAGS -DNDEBUG" ;;
  esac
fi

dnl Checks for libraries.

dnl Checks for header files.
AC_CHECK_HEADERS([stddef.h stdlib.h string.h wchar.h])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
dnl AC_FUNC_MALLOC

AC_OUTPUT(
  libthai.pc
  Makefile
  include/Makefile
  include/thai/Makefile
  src/Makefile
  src/thctype/Makefile
  src/thstr/Makefile
  src/thcell/Makefile
  src/thinp/Makefile
  src/thrend/Makefile
  src/thcoll/Makefile
  src/thbrk/Makefile
  src/thwchar/Makefile
  src/thwctype/Makefile
  src/thwbrk/Makefile
  tests/Makefile
  man/Makefile
  man/man3/Makefile
)

AC_MSG_RESULT([Type make to build libthai.])

